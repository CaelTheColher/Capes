buildscript {
    dependencies {
        classpath("org.kohsuke:github-api:1.313")
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.jetbrains.kotlin.jvm" version "1.8.0"
    id "me.shedaniel.unified-publishing"
    id "org.ajoberstar.grgit"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"

    modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

    implementation "com.github.LlamaLad7:MixinExtras:${rootProject.mixinextras_version}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${rootProject.mixinextras_version}"
    include "com.github.LlamaLad7:MixinExtras:${rootProject.mixinextras_version}"

    modImplementation "com.ptsmods:devlogin:3.1.1"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"

    from rootProject.file("LICENSE")
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
String getDisplayName() {
    return "[Forge ${rootProject.minecraft_version}] Capes ${project.version.toString().split('\\+')[0]}}"
}

String getChangelog() {
    "A changelog can be found at https://github.com/CaelTheColher/Capes"
}

def getBranch() {
    def env = System.getenv()
    if (env.GITHUB_REF) {
        def branch = env.GITHUB_REF
        return branch.substring(branch.lastIndexOf("/") + 1)
    }

    if (grgit == null) {
        return "unknown"
    }

    def branch = grgit.branch.current().name
    return branch.substring(branch.lastIndexOf("/") + 1)
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub

tasks.register('github') {
    dependsOn remapJar
    def env = System.getenv()
    onlyIf {
        env.GITHUB_TOKEN
    }

    doLast {
        def github = GitHub.connectUsingOAuth(env.GITHUB_TOKEN as String)
        def repository = github.getRepository(env.GITHUB_REPOSITORY)

        def releaseBuilder = new GHReleaseBuilder(repository, version as String)
        releaseBuilder.name(getDisplayName())
        releaseBuilder.body(getChangelog())
        releaseBuilder.commitish(getBranch())

        def ghRelease = releaseBuilder.create()
        ghRelease.uploadAsset(tasks.remapJar.archiveFile.get().getAsFile(), "application/java-archive")
    }
}
unifiedPublishing {
    project {
        displayName = getDisplayName()
        releaseType = "release"
        gameVersions = ["1.19.3"]
        gameLoaders = ["fabric"]
        changelog = getChangelog()

        mainPublication tasks.remapJar // Declares the publicated jar

        var cfToken = System.getenv("CURSEFORGE_API_KEY")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = rootProject.curseforge_id // Required, must be a string, ID of CurseForge project

                relations {
                    depends "fabric-language-kotlin"
                    depends "fabric-api"
                    optional "modmenu"
                }
            }
        }

        var mrToken = System.getenv("MODRINTH_TOKEN")
        if (mrToken != null) {
            modrinth {
                token = mrToken
                id = rootProject.modrinth_id // Required, must be a string, ID of Modrinth project

                relations {
                    depends "fabric-language-kotlin"
                    depends "fabric-api"
                    optional "modmenu"
                }
            }
        }
    }
}

rootProject.tasks.named("modPublish").get().dependsOn tasks.named("publishUnified").get()
rootProject.tasks.named("modPublish").get().dependsOn tasks.named("github").get()