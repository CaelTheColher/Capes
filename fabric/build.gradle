buildscript {
    dependencies {
        classpath("org.kohsuke:github-api:1.313")
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.jetbrains.kotlin.jvm"
    id "org.ajoberstar.grgit"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"

    modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

    modImplementation "com.ptsmods:devlogin:3.5"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set "fabric"

    from rootProject.file("LICENSE")
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

def DISPLAY_NAME = "[Fabric ${minecraft_version}] Capes ${mod_version.split('\\+')[0]}"

static def getChangeLog() {
    return "A changelog can be found at https://github.com/CaelTheColher/Capes"
}

curseforge {
    String token = System.getenv("CURSEFORGE_API_KEY")
    apiKey = token == null || token.isEmpty() ? "unset" : token
    project {
        id = curseforge_id
        changelog = getChangeLog()
        releaseType = "release"
        addGameVersion("Fabric")
        addGameVersion(minecraft_version)
        relations {
            requiredDependency("fabric-api")
            requiredDependency("fabric-language-kotlin")
            optionalDependency("modmenu")
        }
        mainArtifact(tasks.remapJar.archiveFile.get().getAsFile()) {
            displayName = DISPLAY_NAME
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    curseGradleOptions.forgeGradleIntegration = false
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_id
    versionNumber = mod_version
    versionName = DISPLAY_NAME
    uploadFile = remapJar
    gameVersions = [minecraft_version]
    loaders = ['fabric']
    versionType = "release"
    changelog = getChangeLog()
    dependencies {
        required.project("fabric-api")
        required.project("fabric-language-kotlin")
        optional.project("modmenu")
    }
}

def getBranch() {
    def env = System.getenv()
    if (env.GITHUB_REF) {
        def branch = env.GITHUB_REF
        return branch.substring(branch.lastIndexOf("/") + 1)
    }

    if (grgit == null) {
        return "unknown"
    }

    def branch = grgit.branch.current().name
    return branch.substring(branch.lastIndexOf("/") + 1)
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub

tasks.register('github') {
    dependsOn remapJar
    def env = System.getenv()
    onlyIf {
        env.GITHUB_TOKEN
    }

    doLast {
        def github = GitHub.connectUsingOAuth(env.GITHUB_TOKEN as String)
        def repository = github.getRepository(env.GITHUB_REPOSITORY)

        def releaseBuilder = new GHReleaseBuilder(repository, "fabric-" + version as String)
        releaseBuilder.name(DISPLAY_NAME)
        releaseBuilder.body(getChangeLog())
        releaseBuilder.commitish(getBranch())

        def ghRelease = releaseBuilder.create()
        ghRelease.uploadAsset(tasks.remapJar.archiveFile.get().getAsFile(), "application/java-archive")
    }
}

tasks.named("curseforge").get().dependsOn tasks.named("remapJar").get()
tasks.named("modrinth").get().dependsOn tasks.named("remapJar").get()

rootProject.tasks.named("fabricPublish").get().dependsOn tasks.named("curseforge").get()
rootProject.tasks.named("fabricPublish").get().dependsOn tasks.named("modrinth").get()
rootProject.tasks.named("fabricPublish").get().dependsOn tasks.named("github").get()